/******************************************************************************
Copyright (c) W.J. van der Laan

Permission is hereby granted, free of charge, to any person obtaining a copy of 
this software  and associated documentation files (the "Software"), to deal in 
the Software without restriction, including without limitation the rights to use, 
copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
Software, and to permit persons to whom the Software is furnished to do so, subject 
to the following conditions:

The above copyright notice and this permission notice shall be included in all copies 
or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, 
INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A 
PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT 
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION 
OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,OUT OF OR IN CONNECTION WITH THE 
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
******************************************************************************/
/** Deferred shading framework
	// W.J. :wumpus: van der Laan 2005 //
	
	Post shader: Light geometry material
*/

#define LIGHT_POINT 		1
#define LIGHT_SPOT			2
#define LIGHT_DIRECTIONAL	3
#define SHADOW_INTENSITY	0.5f		//[0,1]
#define SPECULAR_MULTIPLIER 2.0f
#define EMISSIVE_MULTIPLIER	5.0F

//////////////////////////////////////////////////////////////////////////////
// Helper function section
//////////////////////////////////////////////////////////////////////////////

//DirectX needs this to compensate for jitter
ps_2_x float2 fixUV(float2 texCoord, float2 texSize)
{
	return texCoord - (float2(0.5,0.5)/texSize);
}

float2 fixUV(float2 texCoord, float2 texSize)
{
	return texCoord;
}

//////////////////shadow

float calcPSSMDepthShadow(sampler2D shadowMap0, sampler2D shadowMap1, sampler2D shadowMap2, 
						   float4 lsPos0, float4 lsPos1, float4 lsPos2,
						   float4 pssmSplitPoints, float camDepth)
{

	float shadow;
	float4 splitColour;
	// calculate shadow
	if (camDepth <= pssmSplitPoints.y)
	{
		splitColour = float4(0.3, 0.0, 0, 0);
		shadow = tex2Dproj(shadowMap0, lsPos0).x >= lsPos0.z ? 1 : 1-SHADOW_INTENSITY;
	}
	else if (camDepth <= pssmSplitPoints.z)
	{
		splitColour = float4(0, 0.3, 0, 0);
		shadow = tex2Dproj(shadowMap1, lsPos1).x >= lsPos1.z ? 1 : 1-SHADOW_INTENSITY;
	}
	else
	{
		splitColour = float4(0.0, 0.0, 0.3, 0);
		shadow = tex2Dproj(shadowMap2, lsPos2).x >= lsPos2.z ? 1 : 1-SHADOW_INTENSITY;
	}

	return shadow;
}

//////////////////////////////////////////////////////////////////////////////
// Main shader section
//////////////////////////////////////////////////////////////////////////////	
	
float4 main(

#if LIGHT_TYPE == LIGHT_DIRECTIONAL
	float2 texCoord : TEXCOORD0, 
	float3 ray : TEXCOORD1,
#else
	float4 projPos : TEXCOORD0,
#endif
	
	uniform sampler Tex0: register(s0),
	uniform sampler Tex1: register(s1),
	uniform sampler Tex2: register(s2),
	uniform sampler Tex3: register(s3),

#if LIGHT_TYPE != LIGHT_POINT
	uniform float3 lightDir,
#endif

#if LIGHT_TYPE == LIGHT_SPOT
	uniform float4 spotParams,
#endif

#if LIGHT_TYPE != LIGHT_DIRECTIONAL
	uniform float vpWidth,
	uniform float vpHeight,
	uniform float3 farCorner,
	uniform float flip,
#endif

#ifdef IS_SHADOW_CASTER
	uniform float4x4 invView,
	uniform float4x4 shadowViewProjMat,
	uniform sampler ShadowTex : register(s4),
	uniform sampler ShadowTex2 : register(s5),
	uniform sampler ShadowTex3 : register(s6),
	uniform float3 shadowCamPos,
	uniform float shadowFarClip,
#endif

	uniform float farClipDistance,
	// Attributes of light
	uniform float4 lightDiffuseColor,
	uniform float4 lightSpecularColor,
	uniform float4 lightFalloff,
	uniform float3 lightPos

	,uniform float4x4 projMatrix
	//阴影参数
	, uniform float4 depthRange0 // x = min, y = max, z = range, w = 1/range
	, uniform float4 depthRange1 // x = min, y = max, z = range, w = 1/range
	, uniform float4 depthRange2 // x = min, y = max, z = range, w = 1/range
	, uniform float4x4 texWorldViewProjMatrix0
	, uniform float4x4 texWorldViewProjMatrix1
	, uniform float4x4 texWorldViewProjMatrix2
	, uniform float4 pssmSplitPoints
	) : COLOR 
{

	//None directional lights have some calculations to do in the beginning of the pixel shader
#if LIGHT_TYPE != LIGHT_DIRECTIONAL
	projPos /= projPos.w;
	// -1 is because generally +Y is down for textures but up for the screen
    float2 texCoord = float2(projPos.x, projPos.y * -1 * flip) * 0.5 + 0.5;
	// Texture coordinate magic, this compensates for jitter
	texCoord = fixUV(texCoord, float2(vpWidth, vpHeight));
	float3 ray = float3(projPos.x, projPos.y * flip, 1) * farCorner;
#endif
	
	float4 diffuse = tex2D(Tex0, texCoord);				// Attribute 0: Diffuse color + material ID
	float4 emissive = tex2D(Tex1, texCoord);			// Attribute 1: Emissive color
	float4 specular = tex2D(Tex2, texCoord);			// Attribute 2: Specular color + shiness
	float4 normaldepth = tex2D(Tex3, texCoord);			// Attribute 3: Normal + Depth

	float depth = normaldepth.a;
	int materiaiID = diffuse.a * 256;
	float3 normal = normaldepth.xyz;

	// Calculate position of texel in view space
	float3 viewPos = normalize(ray)*depth*farClipDistance;

	// Calculate light direction and distance
#if LIGHT_TYPE == LIGHT_DIRECTIONAL
	float3 objToLightDir = -lightDir.xyz;
#else
	float3 objToLightVec = lightPos - viewPos;
	float len_sq = dot(objToLightVec, objToLightVec);
	float len = sqrt(len_sq);
	float3 objToLightDir = objToLightVec/len;
#endif
	
	// Calculate diffuse colour
	float3 total_light_contrib;
	total_light_contrib = max(0.0,dot(objToLightDir, normal)) * lightDiffuseColor.rgb;

	// Calculate specular component
	if(materiaiID%2 == 1)
	{
		float3 viewDir = -normalize(viewPos);
		float3 h = normalize(viewDir + objToLightDir);
		float3 light_specular = pow(dot(normal, h), specular.w) * specular.rgb;

		total_light_contrib += light_specular * SPECULAR_MULTIPLIER;
	}

	///////////////////TODO: 应该条件判断是否是阴影接受者
	float4 worldPos = mul(invView, float4(viewPos, 1));

	// Calculate the position of vertex in light space
	float4 lightSpacePos0 = mul(texWorldViewProjMatrix0, worldPos);
	float4 lightSpacePos1 = mul(texWorldViewProjMatrix1, worldPos);
	float4 lightSpacePos2 = mul(texWorldViewProjMatrix2, worldPos);

	// make linear
	lightSpacePos0.z = (lightSpacePos0.z - depthRange0.x) * depthRange0.w;
	lightSpacePos1.z = (lightSpacePos1.z - depthRange1.x) * depthRange1.w;
	lightSpacePos2.z = (lightSpacePos2.z - depthRange2.x) * depthRange2.w;

	float shadow = calcPSSMDepthShadow(ShadowTex, ShadowTex2, ShadowTex3, 
		lightSpacePos0, lightSpacePos1, lightSpacePos2,
		pssmSplitPoints, depth);

	total_light_contrib *= shadow;

#if IS_ATTENUATED
	clip(lightFalloff.x - len);
	// Calculate attenuation
	float attenuation = dot(lightFalloff.yzw, float3(1.0, len, len_sq));
	total_light_contrib /= attenuation;
#endif

#if LIGHT_TYPE == LIGHT_SPOT
	float spotlightAngle = saturate(dot(lightDir.xyz, -objToLightDir));
	float spotFalloff = saturate((spotlightAngle - spotParams.x) / (spotParams.y - spotParams.x));
	total_light_contrib *= (1-spotFalloff);
#endif

	float4 final = float4(total_light_contrib * diffuse.rgb, 1.0f);

	//自发光
	if(materiaiID%3 > 1)
	{
		final.rgb += emissive.rgb * emissive.a * EMISSIVE_MULTIPLIER;
	}

	return final;
}
